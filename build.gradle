plugins {
    id 'java-library'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven'
    id 'maven-publish'
}

group 'com.ringcentral'
version '0.1.0'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'com.squareup.okhttp3:okhttp:4.8.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'com.ringcentral:ringcentral:1.4.0'
    testImplementation 'io.github.cdimascio:java-dotenv:5.2.1'
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "tylerlong"
            name "Tyler Liu"
            email "tyler.liu@ringcentral.com"
        }
    }
    scm {
        url "https://github.com/ringcentral/engage-voice-java"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.ringcentral'
            artifactId 'ringcentral-engage-voice'
            version '0.1.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'RingCentral Engage Voice Java SDK')
                root.appendNode('name', 'ringcentral-engage-voice')
                root.appendNode('url', 'https://github.com/ringcentral/engage-voice-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        name = 'ringcentral-engage-voice'
        userOrg = user
        desc = 'RingCentral Engage Voice Java SDK'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/ringcentral/engage-voice-java'
        issueTrackerUrl = 'https://github.com/ringcentral/engage-voice-java/issues'
        vcsUrl = 'https://github.com/ringcentral/engage-voice-java.git'
        labels = ['RingCentral', 'Engage Voice']
        publicDownloadNumbers = true
        githubRepo = 'ringcentral/engage-voice-java' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        version {
            name = '0.1.0'
            desc = 'RingCentral Engage Voice Java SDK 0.1.0'
            vcsTag = '0.1.0'
            gpg {
                sign = true
            }
        }
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}
